<!DOCTYPE html>
<html>

<head>
  <meta charset="UTF-8">
  <title>Gradient descent</title>
  <style type="text/css">
    body {
      margin: 0px;
      padding: 0px;
      background-color: rgb(10, 0, 30);
    }

    #CSCanvas {
      /*16:9
                width: 100vw;
                height: 56.25vw;
                max-height: 100vh;
                max-width: 177.77vh;
                */
      width: 100vw;
      position: absolute;
      /* works if URL */
      top: 0;
      bottom: 0;
      filter: invert(1);
    }
  </style>
  <link rel="stylesheet" href="css/user-input.css">
  <script type="text/javascript" src="../../common/js/Cindy.js"></script>
  <script type="text/javascript" src="../../common/js/url-parameters.js"></script>
  <script type="text/javascript" src="../../common/js/i18n4js-1.3.0.min.js"></script>

  <script id="csinit" type="text/x-cindyscript">
    touch = geturlparameter("touch", false);
    gamepad = geturlparameter("gamepad", false);
    controller = touch % gamepad;
    userdraw = geturlparameter("draw", false);
      autopilot = geturlparameter("autopilot", !userdraw);
      watertheme = geturlparameter("water", false);


      boatpos = 3;

      //waterh = 7.5;

      EPS = 0.2; //how close to minimum counts as won?
      K = 9; //number of fourier coefficents

      updatescreenparameters() := (
          waterh = max(apply(screenbounds(),#.y))-2;
          textposition = (screenbounds()_4).xy+(0.5,2);
          B0 = (screenbounds()_3).xy+(-3,2);
          B0r = 3;
      );
      updatescreenparameters();

      f(x) := (x=pi*x/16; sum(1..K, k, re(r_k)*cos((k-1)*x)+im(r_k)*sin((k-1)*x)));
      df(x) := (x=pi*x/16; pi/16*sum(1..K, k, -(k-1)*re(r_k)*sin((k-1)*x)+(k-1)*im(r_k)*cos((k-1)*x)));

//xsamples = apply(1..(2*K-1),k, 16*2*(k/(2*K)-.5));
xsamples = apply(1..(2*K-1), k, k/(2*K-1)*32-16);
ysamples = apply(xsamples,0);

mat = transpose(
  apply(1..K,k,
    r = apply(1..K,j,if(j==k,1,0));
    //errc(r);
    apply(xsamples, x, f(x))
  ) ++
  apply(2..K,k,
    r = i*apply(1..K,j,if(j==k,1,0));
    //errc(r);
    apply(xsamples, x, f(x))
  )
);

computecoefficients() := (
  pr = r;
  //imat = inverse(mat);
  //errc(mat);
  //r = inverse(mat)*ysamples;
  sol = linearsolve(mat, ysamples);
  //errc(sol);
  r = apply(1..K,k,if(k==1, sol_k, sol_k+i*sol_(K+k-1)));
  r = apply(1..K, k, r_k*1/k^.5); //smooth curve
  r = .5*r+.5*pr;


  //errc(f(best));

  //errc(r)
);
xvals = (-160..160)/10;
reset():=(
  best = -100;
  if(|best|>13, //TODO replace with while
    r = apply(1..K,k,randomnormal()/(k^1.8)+i*randomnormal()/(k^1.8));
    r_1 = r_1/3-.5; //function is lower than avg
    r_2 = r_2-.1;//lift function at boundaries

    //obtain
    forall(xvals, x, if(|f(x)|>|f(best)|, best=x));
    //errc(f(best));
    r = r*if(watertheme, waterh-.5, 7.5)/|f(best)|;//fit screen (and water)

    best = xvals_1;
    forall(xvals, x, if(f(x)<f(best), best=x));
  );
  pts = [];
  mode = "select";
  if(userdraw,
    mode = "draw";
    r = apply(1..K,0);
  );
  N = geturlparameter("draws", 7);
);
reset();

if(autopilot,
  initpt = -8;
  cnt = 0;
  while((|df(initpt)|<1 % f(initpt)<0) & cnt < 10,
    //initpt = randomnormal();
    reset();
    cnt = cnt + 1;
    errc(cnt);
  );
);


dot(x,y):=(x*y);

gradientdescent() := (
  regional(gamma, x);
  x = if(pts==[], initpt,
    gamma = if(length(pts)>=2,
      |dot(pts_(-1)-pts_(-2), df(pts_(-1))-df(pts_(-2)))|/dot(df(pts_(-1))-df(pts_(-2)),df(pts_(-1))-df(pts_(-2))) //from https://en.wikipedia.org/wiki/Gradient_descent
      ,
      1
    );
    if(|gamma|>5, gamma = 5*gamma/|gamma|);//maximum step width
    pts_(-1) - gamma*df(pts_(-1))
  );
  mod(x+16,32)-16
);

l0 = seconds();
t0() := seconds()-l0;
resett0() := (
  l0 = seconds();
);

boatmove(delta) := (
  if(mode=="select" & !autopilot,
    boatpos = max(min(boatpos + delta, 16), -16);
  );
);

boatprobe() := (
  if(mode=="select" & !autopilot & (N-length(pts))>0,
     x = boatpos;
     pts = pts :> x;
     mode = "compute";
     resett0();
  );


  if(mode=="final",
    if(!autopilot,
      initpt = pts_1;
      pts = [];
      mode = "select";
      autopilot = true;
      ,
      reset();
      autopilot = false;
    );
  );
);
      </script>
  <script id="csdraw" type="text/x-cindyscript">
    updatescreenparameters();

//plot(f(x));

x = mouse().x;

msg = "";

if(mode == "draw",
  if(watertheme,
    msg = international("shape-ocean"),
    msg = international("shape-function")
  );
  drawtext(B0, align->"mid", international("done"), bold->|mouse().xy-B0|<B0r);

  connect(transpose([xvals,apply(xvals,x,f(x))]));

  //forall(1..(2*K-1), k, draw([xsamples_k, ysamples_k]));

);
if(autopilot, connect(transpose([xvals,apply(xvals,x,f(x))]),alpha->.2));

if(autopilot & (mode=="select" % mode=="compute"),

  if(watertheme,
    msg = international("approach-deep"),
    msg = international("approach-minimum")
  );

);

if(mode=="select",
  if((N-length(pts))==0, mode="final"; resett0());

  if(autopilot,
    if(t0()>.5,
      x = gradientdescent();
      pts = pts :> x;
      mode = "compute";
      resett0();
    );
    ,
    if(watertheme,
      //msg = "Finde den Schatz an der tiefsten Stelle!",
      //msg = "Approach the minimal function value! Click to guess!"
      msg = international("aim-deep"),
      msg = international("aim-minimum")
    );

  );
  /*if(watertheme,
    msg = msg + newline + "noch $"+(N-length(pts))+"$ Versuch"+if((N-length(pts))==1,"","e"),
    msg = msg + newline + "$"+(N-length(pts))+"$ guess"+if((N-length(pts))==1,"","es")+" left"
  );*/
  msg = msg + newline + international("guess-1") + (N-length(pts))+ " " + international("guess-2", (N-length(pts)));



  if(!autopilot,
    x = if(controller, boatpos, mouse().x);
    ,
    x = if(pts==[], initpt, pts_(-1))*(1-t0()/.5) + gradientdescent()*(t0()/.5);
  );
  y = sin(t0());
  if(watertheme, y = waterh-2+y);
  draw((x,if(watertheme,waterh,0)),(x,y));

  if(watertheme,
    drawtext((x,y),"$?$");
    ,
    drawtext((x,0),"$x$");
    drawtext((x,y),"$f(x)=?$");
  );
);

if(length(pts)>=2,
  yourbest = pts_1;
  forall(pts, x, if(f(x)<f(yourbest),yourbest=x));
  pb = (yourbest,f(yourbest));
  draw(pb+(-5,0),pb+(5,0), color->[0,1,1], alpha->.3);
);

if(mode=="compute",
  speedfactor = max(5,(1+length(pts)));
  if(speedfactor*t0()>2,
  	mode = "select";
    forall(pts,x,
      if(|x-best|<EPS,
        mode = "final";
      );
    );
    resett0();
  );

  x = pts_(-1);
  lt = min(1,speedfactor*t0());
  if(watertheme,
    y = (1-lt)*waterh + lt*f(x);
    draw((x,waterh),(x,y));
    drawtext((x,y),"$?$", alpha->1-lt);
    ,

    y = lt*f(x);

    drawtext((x,0),"$x$");
    drawtext((x,y/10),"$f(x)="+format(f(x),2)+"$", alpha->1-lt);
    draw((x,0),(x,y));
  );

  if(speedfactor*t0()>1,
    df = df(x);
    a = (speedfactor*t0()-1);
    v = (1,df);
    v = .5*v/|v|;
    draw((x,y)-a*v,(x,y)+a*v);
    if(!watertheme,
      drawtext((x,y),"$f'(x)$", alpha->a, angle->arctan2([1,df]), align->"mid");
    );
  );
);


if(mode=="final",
  a = min(1,t0());
  //plot(f(x),alpha->a);
  connect(transpose([xvals,apply(xvals,x,f(x))]), alpha->a);

  yourbest = pts_1;
  forall(pts, x, if(f(x)<f(yourbest),yourbest=x));

  err = |f(best)-f(yourbest)|;

  if(watertheme,
    if(|best-yourbest|<EPS,
      msg = international("found-treasure"); //"Schatz gefunden!"
      ,
      //msg = if(autopilot, "Der Computer hat", "Du hast") + " den Schatz nicht gefunden";
      msg = if(autopilot, international("computer-has"), international("you-have")) + " " + international("not-found-treasure");
    );
    if(!autopilot,
      msg = msg + newline + international("computer-turn");
      ,
      msg = msg + newline + international("your-turn");
    );



    ,
    subjectverb = if(autopilot, international("computer-has"), international("you-have"));
    if(|best-yourbest|<EPS,
      msg = subjectverb + " " + international("reached-global-minimum");
      ,
      msg = subjectverb + " " + international("reached-global-minimum-up-to") +" $" + format(err, 3) + "$.";
    );
    if(!autopilot,
      msg = msg + newline + international("computer-turn");
      ,
      msg = msg + newline + international("your-turn");
    );
  );




  if(watertheme,


      //draw treasure
        translate([best, f(best)+.3]);
        scale(.3);
        tbox = [[-1,-1],[1,-1],[1,1],[-1,1]];
        tcap = [[-1,1],[1,1],[.5,1.4],[0,1.5],[-.5,1.4]];
        angle = pi/4+sin(2*seconds())*.1;
        R2 = [[cos(angle),-sin(angle)],[sin(angle),cos(angle)]];
        R3 = [[cos(seconds()),-sin(seconds())],[sin(seconds()),cos(seconds())]];
        tcap = apply(tcap,t,R2*(t-(-1,1))+(-1,1));
        tgold = [[-.7,1],[.8,1],[.5,1.2],[.2,1.3],[-.4,1.2]];

        fh = mod(.3*seconds(),1);

        gstar = apply(0..9, k, .1*(1+5/3*mod(k,2))*gauss(exp(i*2*pi*k/10)));
        gstar = apply(gstar, t, R3*t+(.3,1.2)+(sin(seconds())*.3,fh));
        drawpoly(tbox, color->[1,1,1]-[.6,.5,.3]);
        fillpoly(tgold, color->[1,1,1]-[.7,.6,.2]);
        fillpoly(gstar, alpha->min(fh,1-fh), color->[1,1,1]-[1,.8,.3]);
        drawpoly(tcap, color->[1,1,1]-[.6,.5,.3]);
        greset();

      ,

    draw((best,f(best)), size->10, alpha->.3*a);
    draw((best,f(best)),(best,f(yourbest)), size->3, alpha->.3*a, color->[0,1,1]);
  );

);


if(watertheme,
  if(mode!="draw",
    boatx = if(controller & !autopilot, boatpos, x);
    boat1  = [[-2.57,0.35],[-2.2,0.35],[-2.16,0.23],[-0.16,0.19],[-0.01,0.43],[2.43,0.56],[1.97,-0.35],[-1.83,-0.32],[-1.92,-0.19],[-2.57,-0.04]]*.5;
    boat2  = [[-1.11,0.19],[-1.11,0.45],[-0.27,0.45],[-0.27,0.68],[0.36,0.68],[0.36,0.92],[0.99,0.92],[0.99,0.47],[-0.01,0.43],[-0.16,0.19]]*.5;
    boat3  = [[-0.75,0.45],[-0.65,0.92],[-0.39,0.97],[-0.39,0.45]]*.5;
    boat4  = [[0.51,0.93],[0.51,1.55]]*.5;
    angle = sin(4*seconds())*.025;
    R = [[cos(angle),-sin(angle)],[sin(angle),cos(angle)]];
    drawpoly(apply(boat1,R*#+(boatx,waterh)));
    drawpoly(apply(boat2,R*#+(boatx,waterh)));
    drawpoly(apply(boat3,R*#+(boatx,waterh)));
    drawpoly(apply(boat4,R*#+(boatx,waterh)));
  );

  watersize = .1;

  connect(transpose([xvals,apply(xvals,x,waterh+watersize*sin(seconds())*sin(x/watersize-seconds())*0.5)]), color->[.6,.6,.3]);
);

forall(if(mode!="compute",1..length(pts),1..(length(pts)-1)), k,
  x = pts_k;
  y = f(x);
  df = df(x);
  c = hue(k*sqrt(.3))*.7;
  draw((x,y),color->c);
  v = (1,df);
  v = .5*v/|v|;
  draw((x,y)-v,(x,y)+v,color->c);
);

drawtext(textposition, msg, size->15);
      </script>
  <script id="csmousedown" type="text/x-cindyscript">
    if(mode=="select" & !autopilot & !controller & (N-length(pts))>0,
   x = mouse().x;
   pts = pts :> x;
   mode = "compute";
   resett0();
);

if(mode=="final" & !controller,
  if(!autopilot,
    initpt = pts_1;
    pts = [];
    mode = "select";
    autopilot = true;
    ,
    reset();
    autopilot = false;
  );
);

if(mode == "draw",
  if(|mouse().xy-B0|<B0r, mode = "select");
);

      </script>
  <script id="csmousedrag" type="text/x-cindyscript">
    if(mode=="draw",
        closestk = 1;
        x = mouse().x;
        y = mouse().y;
        forall(2..(2*K-1), k, if(|xsamples_k-x|<|xsamples_closestk-x|, closestk = k));
        ysamples_closestk=y;
        //xsamples = xsamples_(2..K) ++ [x];
        //ysamples = ysamples_(2..K) ++ [y];
        computecoefficients();
      );
  </script>
  <script id="csmouseup" type="text/x-cindyscript">
    if(mode=="draw",
    forall(xvals, x, if(|f(x)|>|f(best)|, best=x));
    M = if(watertheme, waterh-.5, 7.5);
    if(|f(best)|>M,
      r = r*M/|f(best)|;//fit screen (and water)
    );
    forall(xvals, x, if(f(x)<f(best), best=x));
  );
  </script>
  <script type="text/javascript">
    var ready = CindyJS.waitFor('i18n');
    var translations = {};
    IMAGINARY.i18n.init().then(function(lang){
      translations[lang] = IMAGINARY.i18n.getStrings();
      ready();
    }).catch(function(err){
      console.log("Error loading translation: " + err);
      throw err;
    });

    var cdy = CindyJS({
      language: IMAGINARY.i18n.getLang(),
      translations: translations,
      scripts: "cs*",
      geometry: [

      ],
      ports: [{
        id: "CSCanvas",
        transform: [{
          visibleRect: [-16, -9, 16, 9]
        }],
        virtualwidth: 1024
      }],
      autoplay: true,
      use: ["url-parameters"]
    });
  </script>
</head>

<body>
  <div id="CSCanvas"></div>
  <script type="text/javascript" src="js/user-input.js"></script>
</body>

</html>
