<!DOCTYPE html>
    <html>
    <head>
        <meta charset="UTF-8">

        <title>Unsupervised learning</title>
        <link rel="stylesheet" href="unsupervised-style.css">
        <script type="text/javascript" src="common/js/Cindy.js"></script>
        <script src="https://cdn.jsdelivr.net/npm/@tensorflow/tfjs@1.0.0/dist/tf.min.js"></script>
        <script src="simple-tf-network-plugin.js"></script>
        <script src="url-parameters.js"></script>

      <script id="csinit" type="text/x-cindyscript">
      //nn
        N = [4, 6, 2];
        W = [
          apply(1..N_1, apply(1..N_2, 0)),
          apply(1..N_2, 0),
          apply(1..N_2, apply(1..N_3, 0)),
          apply(1..N_3, 0)
        ];

        pos(k, j) := (
          (k-2)*5,-3*(j-(N_k+1)/2)/sqrt(N_k)
        );

        computing = false;
      //reset nn and history of saved observations
      resethistory():=(
        forall(1..5,k,
          createimage("cimg"+k,160,90);
        );
        computerguess = apply(1..5,"");
        cimgcnt = 0;
        xs = [];
        ys = [];
        cnt = 0;
        //xs = [[1,0,0,0],[0,1,0,0]];
        //ys = [[1,0],[0,1]];
        //xs = [[1,1,1,1],[1,1,1,1]];
        //ys = [[1,0],[0,1]];
        resetweights();
        ncorrect = nfalse = nstrike = 0;

      );
      resethistory();



    /*
        //level 1
        n = 30;
        v0 = (0,0);
        hist = apply(1..n, (0,0));
        v1 = (randomnormal(),randomnormal());

        p = (0,0);
        dt = 0;
        lastseconds = 0;
*/

        ncorrect = nfalse = nstrike = 0;

        generaterandomx() := (
          t0 = random(1000);
          t1 = random(500);
          t2 = random(1000);
          t3 = random(500);
          t4 = random(500);
          t5 = random(500);
          getx()
        );

        click(ans) := (
          if(t9()>1,
            x = getx();
            if(answer(x) == ans, correct(), incorrect());
          );
          javascript("hide([1,2])");
          reset9();
        );

        forall(0..9,k,
          parse("t"+k+"() := ( seconds()-t"+k+";);reset"+k+"() := (t"+k+" = seconds();); t"+k+" = seconds();");
        );
        lt0 = t0;

        msg = "";

        correct() := (
          ncorrect = ncorrect + 1;
          nstrike = nstrike + 1;
          if(mode=="user",
            playsin(440*2^(nstrike/12), damp->4, line->2, amp->.2);
          );


          msg = "your answer was correct " + ["ðŸ˜ƒ","ðŸ˜„","ðŸ˜€","ðŸ˜","ðŸ™ƒ","ðŸ˜Š","ðŸ¤—"]_(randomint(7)+1);
          if(nstrike > 1,
            msg = msg + newline + "("
            + if(nstrike<=6,["first","second","third","fourth","fifth","sixth"]_nstrike,
                if(nstrike + if(mod(nstrike, 10)==1 & nstrike!=11, "st", if(mod(nstrike, 10)==2 & nstrike!=12, "nd", if(mod(nstrike, 10)==3 & nstrike!=13, "rd", "th")))))
            + " time)" + newline
            + "Can you make " + if(required<=9,["one", "two", "three", "four", "five", "six", "seven", "eigth", "nine", "ten"]_required, required)+ " correct guesses in a row?";
          );

          if(nstrike>=required & mode=="user",
            msg = "";
            mode = "menu";
            javascript("hide([1,2,3]); show([3,4,5]); movecenter([3,4,5]);");
          );

          reset9();
        );

        restart() := (
          resetclock();
          javascript("hide([3,4,5]); show([1,2])");
          if(level==1,
              labels = ["biru", "merah"];//blue  red in Indonesian
          );
          if(level==2,
              labels = ["kiri", "kanan"];//left right in Indonesian
          );
          if(level==3,
              labels = ["genap", "gasal"];//even odd in Indonesian
          );
          javascript("setlabels(['"+labels_1+"','"+labels_2+"'])");
          required = geturlparameter("required", 4);
          lt3 = t3();
          ncorrect = nstrike = nfalse = 0;
          reset0();reset1();reset2();reset9();
          msg = "";
          mode = "user";
        );

        nextlevel() := (
          level = mod(level,3) + 1;
          resethistory();
          restart();
        );

        usecomputer() := (
          javascript("hide([1,2,5]); show([3,4]); moveright([3,4]);");
          resethistory();
          //required = 8;
          reset7();
          reset8();
          reset9();
          mode = "computer";
        );

        incorrect() := (
          msg = "your answer was not correct " + ["ðŸ˜¢","ðŸ™„","ðŸ˜•","ðŸ˜®","ðŸ˜ž"]_(randomint(5)+1);
          if(mode=="user",
            playsin(440*2^(-5/12), damp->4, line->2, amp->.2);
          );
          nstrike = 0;
          nfalse = nfalse + 1;
        );
        randomint = 3;
        dir = randomnormal();



        getx() := (
          regional(x);
          if(level==1,
            cval = if(mod(t0()/2,2)>1,1,0);
            x = hue(cval*sqrt(3)) ++ [mod(t0()/2,1)];
          );
          if(level==2,
            /*
            v = t0()*v1+(1-t0())*v0;
            dt = t3();
            reset3();
            if(|p|>6,p=p/|p|*6);//bound
            p = p+v*dt;
            hist = [p] ++ hist_(1..(n-1));
            errc(hist);
            if(t0()>1,
              reset0();
              v0 = v1;
              v1 = -p+4*(randomnormal(),randomnormal());
              if(|v1|>6, v1 = 6*v1/|v1|); //limit speed
            );*/
            //x = hist_(1) ++ hist_(-1);
            x = 4*[sin(t3()/2), cos(t3())]++4*[sin(t3()),cos(t3()/1.1234)]

          );
          if(level==3,
            //if(t0()>2,
            if(floor(t0()/2)!=floor(lt0/2),
              randomint = 2+randomint(4);//round(2+2*|randomnormal()|);

              dir = randomnormal();
            );
            lt0 = t0();
              //reset0();
            //);
            x = [randomint/8, dir, mod(t0()/2,1), mod(randomint*0.43+.3,1)];
          );
          x
        );
        answer(x) := (
          regional(ans);
          if(level==1,
            ans = (|x_[1,2,3]-hue(0)|<.1);
          );
          if(level==2,
            ans = ((x_1-x_3)>0);
          );
          if(level==3,
            randomint = x_1*8;
            ans = mod(randomint,2)==1;
          );
          ans
        );
        drawit(x):= (
            if(level==1,
              fillcircle((0,0),4*x_4+1, alpha->1-(x_4-.5)^2*4, color->x_[1,2,3]);
            );
            if(level==2,
              /*al = 1;
              forall(hist, p,
                draw(p, size->20, alpha->.2*al);
                al = al*.95;
              );

              draw(p, size->20);
              lastseconds = seconds();
              playsin(100*|v|, amp->.1);*/

              draw(x_[1,2], x_[3,4], color->[.5,.5,.5],size->10);
              fillcircle(x_[3,4], color->[1,1,1], 1.5);
              fillcircle(x_[3,4], color->[.3,.2,.4], 1.4);
              fillcircle(x_[1,2], color->[1,1,1], 1.5);
              fillcircle(x_[1,2], color->[.5,1,.1], 1.4);
              if(mode=="user",
                //  x = 4*[sin(t3()/2), cos(t3())]++4*[sin(t3()),cos(t3()/1.1234)]
                v1 = |[cos(t3()/2)/2, sin(t3())]|;
                v2 = |[cos(t3()),sin(t3()/1.1234)/1.1234]|;
                playsin(300*v1, line->"x12", amp->.1);
                playsin(300*v2, line->"x34", amp->.1);
              );


            );
            if(level==3,
              randomint = x_1*8;
              param = x_3;
              dir = x_2;
              forall(0..randomint, k,
                //param =t0()/2;
                draw((2+param)*gauss(exp(2*pi*i*k/randomint+i*dir*.7*(param+10))), alpha->1-.9*(param-.5)^2*4, color->hue(x_4), size->30);
              );
            );
        );

        //applet
          level = geturlparameter("level", 1);
          restart();



      </script>
      <script id="csdraw" type="text/x-cindyscript">

      if(mode!="menu",
        drawtext(color->[1,1,1],(-10,8), align->"right",
          "level: " + newline +
          "correct: " + newline +
          "false: " + newline +
          "correct answers in a row: ");

          drawtext(color->[1,1,1],(-9.7,8), align->"left",
            level + newline +
            ncorrect + newline +
            nfalse + newline +
            nstrike + if(mode=="user","/" + required,""));
      );

      if(mode=="menu",
        drawit(getx())
      );

      if(mode=="user",
        drawit(getx());


        if(t9()<2,
          a = (2-t9())/2;
          drawtext(color->[1,1,1],(0,-5), msg, alpha->a, align->"mid", size->50);
        );

        if(t9()>1,
          javascript("show([1,2])");
        );
      );
      if(mode=="computer",
        //draw network
        forall(1..2, k,
          forall(1..N_k, j0,
            forall(1..N_(k+1), j1,
              w = W_(k*2-1)_j0_j1;
              draw(pos(k, j0), pos(k+1, j1), size->min(2,4*w*w), color->hue(if(w>0,.3,.8)));
            );
          );
        );
        forall(1..3, k,
            forall(1..N_k, j,
              fillcircle(pos(k,j), .5, color->[.7,.7,.7], alpha->.5);
              w = if(k>1,W_(2*k-2)_j,0);
              fillcircle(pos(k,j), .4, alpha->|w|, color->hue(if(w>0,.3,.8)));
            );
        );
        drawtext(color->[1,1,1],pos(3,1)+(1,0), labels_1);
        drawtext(color->[1,1,1],pos(3,2)+(1,0), labels_2);
        if(level==1,
          drawtext(color->[1,1,1],pos(1,1)+(-1,0), align->"right", "$\text{color}_\text{red}$");
          drawtext(color->[1,1,1],pos(1,2)+(-1,0), align->"right", "$\text{color}_\text{green}$");
          drawtext(color->[1,1,1],pos(1,3)+(-1,0), align->"right", "$\text{color}_\text{blue}$");
          drawtext(color->[1,1,1],pos(1,4)+(-1,0), align->"right", "size");
        );
        if(level==2,
          drawtext(color->[1,1,1],pos(1,1)+(-1,0), align->"right", "$x_\text{current}$");
          drawtext(color->[1,1,1],pos(1,2)+(-1,0), align->"right", "$y_\text{current}$");
          drawtext(color->[1,1,1],pos(1,3)+(-1,0), align->"right", "$x_\text{previous}$");
          drawtext(color->[1,1,1],pos(1,4)+(-1,0), align->"right", "$y_\text{previous}$");
        );
        if(level==3,
          drawtext(color->[1,1,1],pos(1,1)+(-1,0), align->"right", "number");
          drawtext(color->[1,1,1],pos(1,2)+(-1,0), align->"right", "rotation");
          drawtext(color->[1,1,1],pos(1,3)+(-1,0), align->"right", "size");
          drawtext(color->[1,1,1],pos(1,4)+(-1,0), align->"right", "color");
        );


        if(t7()>1, //generate new random image
          x = generaterandomx();
          lx = x;
          cimgcnt = mod(cimgcnt,5) + 1;
          clearimage("cimg"+cimgcnt);
          scale = 1.5;
          canvas((-16,-9)/scale,(16,-9)/scale,"cimg"+cimgcnt,
              drawit(x);
          );
          reset7();

          y = [0,0];
          //make some guess for the image based on nn
          predict([x],
            y = #_1;
            //computerguess(y);
            computerguess_cimgcnt = if(y_1>.5, "ya", "tidak") + "  " + if((y_1>.5) == answer(x),"âœ”","âœ—");
            if((y_1>.5) == answer(x), correct(), incorrect());
            /*clearimage("cimg"+cimgcnt);
            canvas((-16,-9)/scale,(16,-9)/scale,"cimg"+cimgcnt,
                fillcircle((0,0),5, color->if((y_1>.5) == answer(x),[0,1,0],[1,0,0]), alpha->.3);
                drawit(x);
            );*/

            //add correct answer to training data
            xs = xs :> x;
            ys = ys :> if(answer(x),[1,0],[0,1]);
            cnt = cnt + 1;
            if(cnt>20 & nstrike<4, resetweights(); cnt = 0;);
            //cut away too old answers.
            if(length(xs)==33, xs=xs_(2..33); ys=ys_(2..33));
          );
        );

        //draw image history
        forall(1..5,k,
          offset = ((mod(-k+cimgcnt,5)-2.5+t7()-.5)*5,-9);
          drawimage(offset+(-3,0),offset+(3,0),"cimg"+k);
          drawtext(color->[1,1,1],offset+(-1.3,1), computerguess_k);
        );


        //train network on computed xs and ys
        if(!computing & t8()>1/15 & length(xs)>=2,
          reset8();
          computing = true;
          train(xs, ys,
            getweights(
              W=#;
              computing = false;
            );
          );
        );
      );


      </script>
    </head>
    <body>
      <div id="container">
        <div id="CSCanvas"></div>
        <div id="button1" class="visible button" onclick="cdy.evokeCS('click(false)')">ya</div>
        <div id="button2" class="visible button" onclick="cdy.evokeCS('click(true)')">tidak</div>

        <div id="button3" class="hidden button center" onclick="cdy.evokeCS('restart()')">restart</div>
        <div id="button4" class="hidden button center" onclick="cdy.evokeCS('nextlevel()')">proceed to next level</div>
        <div id="button5" class="hidden button center" onclick="cdy.evokeCS('usecomputer()')">how the computer would learn?</div>
      </div>
    </body>
    <script type="text/javascript">
      var cdy = CindyJS({
        scripts: "cs*",
        ports: [
          {id: "CSCanvas", transform: [{visibleRect: [-16, -9, 16, 9]}]}
        ],
        use: ["simple-tf-network-plugin", "url-parameters"],
        autoplay: true
      });

      function setlabels(labels) {
        document.getElementById("button1").innerHTML = labels[0];
        document.getElementById("button2").innerHTML = labels[1];
      }
      function hide(l) {
        for(k in l){
          document.getElementById("button"+l[k]).classList.remove("visible");
          document.getElementById("button"+l[k]).classList.add("hidden");
        }
      }

      function show(l) {
        for(k in l){
          document.getElementById("button"+l[k]).classList.remove("hidden");
          document.getElementById("button"+l[k]).classList.add("visible");
        }
      }

      function movecenter(l) {
        for(k in l){
          document.getElementById("button"+l[k]).classList.remove("right");
          document.getElementById("button"+l[k]).classList.add("center");
        }
      }

      function moveright(l) {
        for(k in l){
          document.getElementById("button"+l[k]).classList.remove("center");
          document.getElementById("button"+l[k]).classList.add("right");
        }
      }

    </script>
    </html>
