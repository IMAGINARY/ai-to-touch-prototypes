<!DOCTYPE html>
    <html>
    <head>
        <meta charset="UTF-8">

        <title>Gradient descent</title>
        <style type="text/css">
            body {
                margin: 0px;
                padding: 0px;
                background-color: rgb(10, 0, 30);
            }

            #CSCanvas {
              /*16:9*/
                width: 100vw;
                height: 56.25vw;
                max-height: 100vh;
                max-width: 177.77vh;
                filter: invert(1);
            }
        </style>
        <script type="text/javascript" src="common/js/Cindy.js"></script>
        <script src="url-parameters.js"></script>

      <script id="csinit" type="text/x-cindyscript">
      autopilot = geturlparameter("autopilot", true);
      showwater = geturlparameter("water", false);
      waterh = 7.5;

      f(x) := r0+(x=pi*x/16; sum(1..length(r), k, re(r_k)*cos(k*x)+im(r_k)*sin(k*x)));
      df(x) := (x=pi*x/16; pi/16*sum(1..length(r), k, -k*re(r_k)*sin(k*x)+k*im(r_k)*cos(k*x)));

xvals = (-160..160)/10;
reset():=(
  best = -100;
  r0 = randomnormal();
  if(showwater, r0 = r0-2);
  while(|best|>13,
    r = apply(1..10,k,randomnormal()/(k^1.8)+i*randomnormal()/(k^1.8));
    r_1 = r_1-.8;//lift function at boundaries
    forall(xvals, x, if(|f(x)-r0|>|f(best)-r0|, best=x));
    r = r*(7.5-|r0|)/|f(best)-r0|;//fit screen (and water)


    best = xvals_1;
    forall(xvals, x, if(f(x)<f(best), best=x));

  );
  pts = [];

  mode = "select";
  N = 7;
);
reset();

initpt = -8;
cnt = 0;
while((|df(initpt)|<1 % f(initpt)<0) & cnt < 10,
  //initpt = randomnormal();
  reset();
  cnt = cnt + 1;
  errc(cnt);
);


dot(x,y):=(x*y);

gradientdescent() := (
  regional(gamma, x);
  x = if(pts==[], initpt,
    gamma = if(length(pts)>=2,
      |dot(pts_(-1)-pts_(-2), df(pts_(-1))-df(pts_(-2)))|/dot(df(pts_(-1))-df(pts_(-2)),df(pts_(-1))-df(pts_(-2))) //from https://en.wikipedia.org/wiki/Gradient_descent
      ,
      1
    );
    if(|gamma|>5, gamma = 5*gamma/|gamma|);//maximum step width
    pts_(-1) - gamma*df(pts_(-1))
  );
  mod(x+16,32)-16
);

l0 = seconds();
t0() := seconds()-l0;
resett0() := (
  l0 = seconds();
);
      </script>
      <script id="csdraw" type="text/x-cindyscript">
//plot(f(x));

x = mouse().x;

if(autopilot, connect(transpose([xvals,apply(xvals,x,f(x))]),alpha->.2));

if(autopilot & (mode=="select" % mode=="compute"),
  drawtext((-15,8), "The computer tries to approach the minimum through gradient descent.");
);

if(mode=="select",
  if((N-length(pts))==0, mode="final"; resett0());

  if(autopilot,
    if(t0()>.5,
      x = gradientdescent();
      pts = pts :> x;
      mode = "compute";
      resett0();
    );
    ,
    drawtext((-15,8), "Approach the minimal function value! Click to guess!");
  );
  drawtext((-15,7), "$"+(N-length(pts))+"$ guess"+if((N-length(pts))==1,"","es")+" left");

  if(!autopilot,
    x = mouse().x;
    ,
    x = if(pts==[], initpt, pts_(-1))*(1-t0()/.5) + gradientdescent()*(t0()/.5);
  );
  y = sin(t0());
  if(showwater, y = waterh-2+y);
  draw((x,if(showwater,waterh,0)),(x,y));

  if(showwater,
    drawtext((x,y),"$?$");
    ,
    drawtext((x,0),"$x$");
    drawtext((x,y),"$f(x)=?$");
  );
);

if(length(pts)>=2,
  yourbest = pts_1;
  forall(pts, x, if(f(x)<f(yourbest),yourbest=x));
  pb = (yourbest,f(yourbest));
  draw(pb+(-5,0),pb+(5,0), color->[0,1,1], alpha->.3);
);

if(mode=="compute",
  speedfactor = max(5,(1+length(pts)));
  if(speedfactor*t0()>2,
  	mode = "select";
    resett0();
  );

  x = pts_(-1);
  lt = min(1,speedfactor*t0());
  if(showwater,
    y = (1-lt)*waterh + lt*f(x);
    draw((x,waterh),(x,y));
    drawtext((x,y),"$?$", alpha->1-lt);
    ,

    y = lt*f(x);

    drawtext((x,0),"$x$");
    drawtext((x,y/10),"$f(x)="+format(f(x),2)+"$", alpha->1-lt);
    draw((x,0),(x,y));
  );

  if(speedfactor*t0()>1,
    df = df(x);
    a = (speedfactor*t0()-1);
    v = (1,df);
    v = .5*v/|v|;
    draw((x,y)-a*v,(x,y)+a*v);
    drawtext((x,y),"$f'(x)$", alpha->a, angle->arctan2([1,df]), align->"mid");
  );
);

if(mode=="final",
  a = min(1,t0());
  //plot(f(x),alpha->a);
  connect(transpose([xvals,apply(xvals,x,f(x))]), alpha->a);

  yourbest = pts_1;
  forall(pts, x, if(f(x)<f(yourbest),yourbest=x));

  err = |f(best)-f(yourbest)|;

  subjectverb = if(autopilot, "The computer has reached", "You have reached");
  if(err<.02,
    drawtext((-15,8), subjectverb + " the global minimum.");
    ,
    drawtext((-15,8), subjectverb + " the global minimum up to $" + format(err,3) + "$.");
  );
  if(!autopilot,
    drawtext((-15,7), "Click to see what the computer would have done.");
    ,
    drawtext((-15,7), "Now it is your turn! Click to restart the applet with another function.");
  );


  draw((best,f(best)), size->10, alpha->.3*a);
  draw((best,f(best)),(best,f(yourbest)), size->3, alpha->.3*a, color->[0,1,1]);
);


if(showwater,
  boat  = [[-2,-1],[2,-1],[2.5,1],[-2.5,1]]*.5;
  angle = sin(4*t0())*.04;
  R = [[cos(angle),-sin(angle)],[sin(angle),cos(angle)]];

  drawpoly(apply(boat,R*#+(x,waterh)));

  watersize = .2;
  connect(transpose([xvals,apply(xvals,x,waterh+watersize*sin(seconds())*sin(x/watersize-seconds()))]), color->[.6,.6,.3]);
);

forall(if(mode!="compute",1..length(pts),1..(length(pts)-1)), k,
  x = pts_k;
  y = f(x);
  df = df(x);
  c = hue(k*sqrt(.3))*.7;
  draw((x,y),color->c);
  v = (1,df);
  v = .5*v/|v|;
  draw((x,y)-v,(x,y)+v,color->c);
);

      </script>
      <script id="csmousedown" type="text/x-cindyscript">
if(mode=="select" & !autopilot & (N-length(pts))>0,
   x = mouse().x;
   pts = pts :> x;
   mode = "compute";
   resett0();
);

if(mode=="final",
  if(!autopilot,
    initpt = pts_1;
    pts = [];
    mode = "select";
    autopilot = true;
    ,
    reset();
    autopilot = false;
  );

);

      </script>

        <script type="text/javascript">
          var cdy = CindyJS({
            scripts: "cs*",
            geometry: [

            ],
            ports: [
              {id: "CSCanvas", transform: [{visibleRect: [-16, -9, 16, 9]}]}
            ],
            autoplay: true,
            use: ["url-parameters"]
          });
        </script>
    </head>
    <body>
        <div id="CSCanvas"></div>
    </body>
    </html>
