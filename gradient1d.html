<!DOCTYPE html>
    <html>
    <head>
        <meta charset="UTF-8">

        <title>Gradient descent</title>
        <style type="text/css">
            body {
                margin: 0px;
                padding: 0px;
                background-color: rgb(10, 0, 30);
            }

            #CSCanvas {
              /*16:9
                width: 100vw;
                height: 56.25vw;
                max-height: 100vh;
                max-width: 177.77vh;
                */
                width: 100vw;
                position: absolute; /* works if URL */
                top: 0;
                bottom: 0;
                filter: invert(1);
            }
        </style>
        <script type="text/javascript" src="common/js/Cindy.js"></script>
        <script src="url-parameters.js"></script>

      <script id="csinit" type="text/x-cindyscript">
      autopilot = geturlparameter("autopilot", true);
      watertheme = geturlparameter("water", false);
      //waterh = 7.5;

      EPS = 0.2; //how close to minimum counts as won?

      updatescreenparameters() := (
          waterh = max(apply(screenbounds(),#.y))-1;
          textposition = (screenbounds()_4).xy+(3,2);
      );
      updatescreenparameters();

      f(x) := r0+(x=pi*x/16; sum(1..length(r), k, re(r_k)*cos(k*x)+im(r_k)*sin(k*x)));
      df(x) := (x=pi*x/16; pi/16*sum(1..length(r), k, -k*re(r_k)*sin(k*x)+k*im(r_k)*cos(k*x)));

xvals = (-160..160)/10;
reset():=(
  best = -100;
  r0 = randomnormal();
  if(watertheme, r0 = r0-1);
  while(|best|>13,
    r = apply(1..10,k,randomnormal()/(k^1.8)+i*randomnormal()/(k^1.8));
    r_1 = r_1-.8;//lift function at boundaries
    forall(xvals, x, if(|f(x)-r0|>|f(best)-r0|, best=x));
    r = r*(7.5-|r0|)/|f(best)-r0|;//fit screen (and water)


    best = xvals_1;
    forall(xvals, x, if(f(x)<f(best), best=x));

  );
  pts = [];

  mode = "select";
  N = 7;
);
reset();

initpt = -8;
cnt = 0;
while((|df(initpt)|<1 % f(initpt)<0) & cnt < 10,
  //initpt = randomnormal();
  reset();
  cnt = cnt + 1;
  errc(cnt);
);


dot(x,y):=(x*y);

gradientdescent() := (
  regional(gamma, x);
  x = if(pts==[], initpt,
    gamma = if(length(pts)>=2,
      |dot(pts_(-1)-pts_(-2), df(pts_(-1))-df(pts_(-2)))|/dot(df(pts_(-1))-df(pts_(-2)),df(pts_(-1))-df(pts_(-2))) //from https://en.wikipedia.org/wiki/Gradient_descent
      ,
      1
    );
    if(|gamma|>5, gamma = 5*gamma/|gamma|);//maximum step width
    pts_(-1) - gamma*df(pts_(-1))
  );
  mod(x+16,32)-16
);

l0 = seconds();
t0() := seconds()-l0;
resett0() := (
  l0 = seconds();
);
      </script>
      <script id="csdraw" type="text/x-cindyscript">
      updatescreenparameters();

//plot(f(x));

x = mouse().x;

msg = "";
if(autopilot, connect(transpose([xvals,apply(xvals,x,f(x))]),alpha->.2));

if(autopilot & (mode=="select" % mode=="compute"),

  if(watertheme,
    msg =  "Der Computer versucht die tiefste Stelle zu finden.",
    msg = "The computer tries to approach the minimum through gradient descent."
  );
);

if(mode=="select",
  if((N-length(pts))==0, mode="final"; resett0());

  if(autopilot,
    if(t0()>.5,
      x = gradientdescent();
      pts = pts :> x;
      mode = "compute";
      resett0();
    );
    ,
    if(watertheme,
      msg = "Finde den Schatz an der tiefsten Stelle!",
      msg = "Approach the minimal function value! Click to guess!"
    );

  );
  if(watertheme,
    msg = msg + newline + "noch $"+(N-length(pts))+"$ Versuch"+if((N-length(pts))==1,"","e"),
    msg = msg + newline + "$"+(N-length(pts))+"$ guess"+if((N-length(pts))==1,"","es")+" left"
  );



  if(!autopilot,
    x = mouse().x;
    ,
    x = if(pts==[], initpt, pts_(-1))*(1-t0()/.5) + gradientdescent()*(t0()/.5);
  );
  y = sin(t0());
  if(watertheme, y = waterh-2+y);
  draw((x,if(watertheme,waterh,0)),(x,y));

  if(watertheme,
    drawtext((x,y),"$?$");
    ,
    drawtext((x,0),"$x$");
    drawtext((x,y),"$f(x)=?$");
  );
);

if(length(pts)>=2,
  yourbest = pts_1;
  forall(pts, x, if(f(x)<f(yourbest),yourbest=x));
  pb = (yourbest,f(yourbest));
  draw(pb+(-5,0),pb+(5,0), color->[0,1,1], alpha->.3);
);

if(mode=="compute",
  speedfactor = max(5,(1+length(pts)));
  if(speedfactor*t0()>2,
  	mode = "select";
    forall(pts,x,
      if(|x-best|<EPS,
        //drawtreasure = true;
        mode = "final";
      );
    );
    resett0();
  );

  x = pts_(-1);
  lt = min(1,speedfactor*t0());
  if(watertheme,
    y = (1-lt)*waterh + lt*f(x);
    draw((x,waterh),(x,y));
    drawtext((x,y),"$?$", alpha->1-lt);
    ,

    y = lt*f(x);

    drawtext((x,0),"$x$");
    drawtext((x,y/10),"$f(x)="+format(f(x),2)+"$", alpha->1-lt);
    draw((x,0),(x,y));
  );

  if(speedfactor*t0()>1,
    df = df(x);
    a = (speedfactor*t0()-1);
    v = (1,df);
    v = .5*v/|v|;
    draw((x,y)-a*v,(x,y)+a*v);
    if(!watertheme,
      drawtext((x,y),"$f'(x)$", alpha->a, angle->arctan2([1,df]), align->"mid");
    );
  );
);

drawtreasure = false;
if(mode=="final",
  a = min(1,t0());
  //plot(f(x),alpha->a);
  connect(transpose([xvals,apply(xvals,x,f(x))]), alpha->a);

  yourbest = pts_1;
  forall(pts, x, if(f(x)<f(yourbest),yourbest=x));

  err = |f(best)-f(yourbest)|;

  if(watertheme,
    if(|best-yourbest|<EPS,
      msg = "Schatz gefunden!";
      ,
      msg = if(autopilot, "Der Computer hat", "Du hast") + " den Schatz nicht gefunden (Fehler: $"+format(err,3)+"$)";
    );
    if(!autopilot,
      msg = msg + newline + "Klicke um zu sehen, wie der Computer gehandelt hÃ¤tte.";
      ,
      msg = msg + newline + "Jetzt bist du am Zug! Klicke, um in einem anderem Meer neu zu beginnen.";
    );



    ,
    subjectverb = if(autopilot, "The computer has reached", "You have reached");
    if(|best-yourbest|<EPS,
      msg = subjectverb + " the global minimum.";
      ,
      msg = subjectverb + " the global minimum up to $" + format(err,3) + "$.";
    );
    if(!autopilot,
      msg = msg + newline + "Click to see what the computer would have done.";
      ,
      msg = msg + newline + "Now it is your turn! Click to restart the applet with another function.";
    );
  );




  if(watertheme,


      //draw treasure
        translate([best, f(best)+.3]);
        scale(.3);
        tbox = [[-1,-1],[1,-1],[1,1],[-1,1]];
        tcap = [[-1,1],[1,1],[.5,1.4],[0,1.5],[-.5,1.4]];
        angle = pi/4+sin(2*seconds())*.1;
        R2 = [[cos(angle),-sin(angle)],[sin(angle),cos(angle)]];
        R3 = [[cos(seconds()),-sin(seconds())],[sin(seconds()),cos(seconds())]];
        tcap = apply(tcap,t,R2*(t-(-1,1))+(-1,1));
        tgold = [[-.7,1],[.8,1],[.5,1.2],[.2,1.3],[-.4,1.2]];

        fh = mod(.3*seconds(),1);

        gstar = apply(0..9, k, .1*(1+5/3*mod(k,2))*gauss(exp(i*2*pi*k/10)));
        gstar = apply(gstar, t, R3*t+(.3,1.2)+(sin(seconds())*.3,fh));
        drawpoly(tbox, color->[1,1,1]-[.6,.5,.3]);
        fillpoly(tgold, color->[1,1,1]-[.7,.6,.2]);
        fillpoly(gstar, alpha->min(fh,1-fh), color->[1,1,1]-[1,.8,.3]);
        drawpoly(tcap, color->[1,1,1]-[.6,.5,.3]);
        greset();

      ,

    draw((best,f(best)), size->10, alpha->.3*a);
    draw((best,f(best)),(best,f(yourbest)), size->3, alpha->.3*a, color->[0,1,1]);
  );

);


if(watertheme,
  boat  = [[-2,-1],[2,-1],[2.5,1],[-2.5,1]]*.5;
  angle = sin(4*seconds())*.04;
  R = [[cos(angle),-sin(angle)],[sin(angle),cos(angle)]];

  drawpoly(apply(boat,R*#+(x,waterh)));

  watersize = .2;


  connect(transpose([xvals,apply(xvals,x,waterh+watersize*sin(seconds())*sin(x/watersize-seconds()))]), color->[.6,.6,.3]);


);

forall(if(mode!="compute",1..length(pts),1..(length(pts)-1)), k,
  x = pts_k;
  y = f(x);
  df = df(x);
  c = hue(k*sqrt(.3))*.7;
  draw((x,y),color->c);
  v = (1,df);
  v = .5*v/|v|;
  draw((x,y)-v,(x,y)+v,color->c);
);

drawtext(textposition, msg);
      </script>
      <script id="csmousedown" type="text/x-cindyscript">
if(mode=="select" & !autopilot & (N-length(pts))>0,
   x = mouse().x;
   pts = pts :> x;
   mode = "compute";
   resett0();
);

if(mode=="final",
  if(!autopilot,
    initpt = pts_1;
    pts = [];
    mode = "select";
    autopilot = true;
    ,
    reset();
    autopilot = false;
  );

);

      </script>

        <script type="text/javascript">
          var cdy = CindyJS({
            scripts: "cs*",
            geometry: [

            ],
            ports: [
              {id: "CSCanvas", transform: [{visibleRect: [-16, -9, 16, 9]}], virtualwidth: 1000}
            ],
            autoplay: true,
            use: ["url-parameters"]
          });
        </script>
    </head>
    <body>
        <div id="CSCanvas"></div>
    </body>
    </html>
